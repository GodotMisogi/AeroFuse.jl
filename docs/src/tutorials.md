```@meta
EditURL = "<unknown>/docs/lit/tutorials.jl"
```

# Tutorials

## Objectives

Here we will teach you the basic functionality of AeroMDAO by showing you how to perform an aerodynamic analysis of a conventional aircraft. We will demonstrate this in a sequential process of complexity. For this, we will need to import some packages which will be convenient.

````@example tutorials
using AeroMDAO      # Main package
using Plots         # Plotting library
gr(dpi = 300)       # Plotting backend
using LaTeXStrings  # For LaTeX printing in plots
````

## Your First Airfoil

You can define a NACA-4 airfoil using the following function. To analyze our airfoil, we must convert the coordinates into a `Foil` type defined in `AeroMDAO`.

````@example tutorials
airfoil = Foil(naca4((2,4,1,2), 60), "NACA 2412")
````

You can access the abscissa and ordinates as the following fields.

````@example tutorials
airfoil.x, airfoil.y
````

You can access the coordinates by calling the following function.

````@example tutorials
coordinates(airfoil)
````

### Geometric Representations
You can convert these coordinates into the camber-thickness representation.

````@example tutorials
xcamthick = camber_thickness(airfoil, 60)
````

You can also do the inverse transformation.

````@example tutorials
coords = camber_thickness_to_coordinates(xcamthick[:,1], xcamthick[:,2], xcamthick[:,3])
````

You can split the coordinates into their upper and lower surfaces.

````@example tutorials
upper, lower = split_surface(airfoil);

x_upper, y_upper = @views upper[:,1], upper[:,2]
x_lower, y_lower = @views lower[:,1], lower[:,2];
nothing #hide
````

### Plotting
Let's plot everything!

````@example tutorials
# Plot object
af_plot = plot(aspect_ratio = 1, xlabel=L"(x/c)", ylabel = L"y")

# Upper surface
plot!(upper[:,1], upper[:,2], label = "$(airfoil.name) Upper",
      ls = :solid, lw = 2, c = :cornflowerblue)

# Lower surface
plot!(lower[:,1], lower[:,2], label = "$(airfoil.name) Lower",
      ls = :solid, lw = 2, c = :orange)

# Camber
plot!(xcamthick[:,1], xcamthick[:,2], label = "$(airfoil.name) Camber",
      ls = :dash, lw = 2, c = :burlywood3)

# Thickness
plot!(xcamthick[:,1], xcamthick[:,3], label = "$(airfoil.name) Thickness",
      ls = :dash, lw = 2, c = :grey)
````

## Your First 2D Aerodynamic Analysis

Now we have an airfoil, and we would like to analyze its aerodynamic characteristics. The potential flow panel method for inviscid analyses of airfoils, which you may have studied in your course in aerodynamics, provides decent estimations of the lift generated by the airfoil.

Our analysis also requires boundary conditions, which is the freestream flow defined by a magnitude ``V_\infty`` and angle of attack ``\alpha``. We provide these to the analysis by defining variables and feeding them to a `Uniform2D` type, corresponding to uniform flow in 2 dimensions.

````@example tutorials
V       = 1.0
alpha   = 4.0
uniform = Uniform2D(V, alpha)
````

Now that we have our airfoil and boundary conditions, we can call the `solve_case()` function, which in this case has an associated method with the specification of ``n`` panels given by the optional argument `num_panels`, which is 60 by default. This will run the analysis and return the lift coefficient, the sectional lift and pressure coefficients, and the panels for the given case.

````@example tutorials
cl, cls, cms, cps, panels = solve_case(airfoil, uniform; wake_length = 1e4, num_panels = 60);
nothing #hide
````

Note the difference between the lift coefficient computed and the sum of the sectional lift coefficients; this is due to numerical errors in the solution procedure and modeling.

````@example tutorials
cl, sum(cls)
````

!!! info
    Support for drag prediction with boundary layer calculations will be added soon. For now, try out the amazing [Webfoil](http://webfoil.engin.umich.edu/) developed by the MDOLab at University of Michigan -- Ann Arbor!

### Visualization

Let's see what the pressure and lift distribution curves look like over the airfoil. AeroMDAO provides more helper functions for post-processing data.

````@example tutorials
pts      = collocation_point.(panels) # Collocation point
tangents = panel_tangent.(panels)     # Tangents
normals  = panel_normal.(panels)      # Normals
locs     = panel_location.(panels);   # Upper or lower surface
nothing #hide
````

You can make your plots fancier by segregating the values depending on the locations of the panels.

````@example tutorials
get_surface_values(panels, vals, surf = "lower") = [ (collocation_point(panel)[1], val) for (val, panel) in zip(vals, panels) if panel_location(panel) == surf ]

cp_lower = get_surface_values(panels, cps, "lower")
cp_upper = get_surface_values(panels, cps, "upper");
nothing #hide
````

Now let's plot the results.

````@example tutorials
plot(yflip = true, xlabel = L"(x/c)", ylabel = L"C_p")
plot!(cp_upper, label = "Upper",
      ls = :dash, lw = 2, c = :cornflowerblue)
plot!(cp_lower, label = "Lower",
      ls = :dash, lw = 2, c = :orange)
plot!(x_upper, -y_upper, label = "$(airfoil.name) Upper",
      ls = :solid, lw = 2, c = :cornflowerblue)
plot!(x_lower, -y_lower, label = "$(airfoil.name) Lower",
      ls = :solid, lw = 2, c = :orange)
````

Lift distribution

````@example tutorials
cl_upper = get_surface_values(panels, cls, "upper")
cl_lower = get_surface_values(panels, cls, "lower")

cl_plot = plot(xlabel = L"(x/c)", ylabel = L"C_l")
plot!(x_upper, y_upper, lw = 2, c = :cornflowerblue, label = "Upper")
plot!(x_lower, y_lower, lw = 2, c = :orange, label = "Lower")
plot!(cl_upper, ls = :dash, lw = 2, c = :cornflowerblue, label = "Cl Upper")
plot!(cl_lower, ls = :dash, lw = 2, c = :orange, label = "Cl Lower")
````

## Your First Wing

Here you will learn how to define a wing using an intuitive parametrization scheme.

### Parametrization

The following parametrization is used for the wing.
![](https://godot-bloggy.xyz/post/diagrams/WingGeometry.svg)

First, we define a `Vector` of `Foil`s.

````@example tutorials
# Define one airfoil
airfoil_1 = Foil(naca4(4,4,1,2))

# Define vector of airfoils
airfoils  = [ airfoil_1, Foil(naca4(0,0,1,2)) ]
````

Now that we have our airfoil profiles in the appropriate type, we can define our wing. The following function defines a symmetric wing and prints the relevant information.

````@example tutorials
wing = Wing(foils     = airfoils,    # Foil profiles
            chords    = [1.0, 0.6],  # Chord lengths
            twists    = [2.0, 0.0],  # Twist angles
            spans     = [4.0],       # Section span lengths
            dihedrals = [5.],        # Dihedral angles
            LE_sweeps = [5.])        # Leading-edge sweep angles
````

!!! note
    See the how-to guide on how to define an asymmetric wing.

The following "getter" functions provide the quantities of interest.

```@repl tutorials
chords(wing)
spans(wing)
twists(wing)
dihedrals(wing)
sweeps(wing)
```

There is also a convenient function for pretty-printing information, in which the first argument takes the `Wing` type and the second takes a name (as a `String` or `Symbol`).

````@example tutorials
print_info(wing, "Wing")
````

### Visualization

The following function generates the coordinates of the wing's outline.

````@example tutorials
wing_outline = plot_wing(wing)
````

Now let's plot it.

````@example tutorials
plt = plot(
           wing_outline[:,1], wing_outline[:,2], wing_outline[:,3],
           label = "Wing",
           xaxis = "x", yaxis = "y", zaxis = "z",
           aspect_ratio = 1,
           camera = (30, 45),
           zlim = (-0.1, span(wing) / 2),
          )
````

## Your First 3D Aerodynamic Analysis

```@repl tutorials
## Wing
wing = Wing(foils     = Foil.(fill(naca4(2,4,1,2), 2)),
            chords    = [1.0, 0.6],
            twists    = [2.0, 0.0],
            spans     = [4.0],
            dihedrals = [5.],
            LE_sweeps = [5.])

## Horizontal tail
htail = Wing(foils     = Foil.(fill(naca4(0,0,1,2), 2)),
             chords    = [0.7, 0.42],
             twists    = [0.0, 0.0],
             spans     = [1.25],
             dihedrals = [0.],
             LE_sweeps = [6.39],
             position  = [4., 0, 0],
             angle     = -2.,
             axis      = [0., 1., 0.])

## Vertical tail
vtail = HalfWing(foils     = Foil.(fill(naca4(0,0,0,9), 2)),
                 chords    = [0.7, 0.42],
                 twists    = [0.0, 0.0],
                 spans     = [1.0],
                 dihedrals = [0.],
                 LE_sweeps = [7.97],
                 position  = [4., 0, 0],
                 angle     = 90.,
                 axis      = [1., 0., 0.])

## Printing information
print_info(wing, "Wing")
print_info(htail, "Horizontal Tail")
print_info(vtail, "Vertical Tail")

## Meshing
wing_mesh  = WingMesh(wing, [12], 6,
                      span_spacing = Cosine()
                     )
htail_mesh = WingMesh(htail, [12], 6,
                      span_spacing = Cosine()
                     )
vtail_mesh = WingMesh(vtail, [12], 6,
                      span_spacing = Cosine()
                     )

## Aircraft assembly
aircraft = ComponentArray(
                          wing  = make_horseshoes(wing_mesh),
                          htail = make_horseshoes(htail_mesh),
                          vtail = make_horseshoes(vtail_mesh)
                         );

## Define freestream condition
fs  = Freestream(alpha = 3.0,
                 beta  = 0.0,
                 omega = [0., 0., 0.]);

## Define reference values
refs = References(speed    = 1.0,
                  area     = projected_area(wing),
                  span     = span(wing),
                  chord    = mean_aerodynamic_chord(wing),
                  density  = 1.225,
                  location = mean_aerodynamic_center(wing));

## Evaluate circulations
system = solve_case(
        aircraft, fs, refs;
        print            = true, # Prints the results for only the aircraft
        print_components = true, # Prints the results for all components
    )

## Evaluate derivatives
dv_data = solve_case_derivatives(
        aircraft, fs, refs;
        print              = false, # Prints the results for only the aircraft
        print_components   = true,  # Prints the results for all components
    );
```

---

*This page was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*

